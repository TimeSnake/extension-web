plugins {
  id 'java'
  id 'java-base'
  id 'java-library'
  id 'maven-publish'
  id('com.github.johnrengelman.shadow') version "8.1.1"
}


group = "de.timesnake"
version = '2.0'
archivesBaseName = 'extension-web'
var projectId = 35

repositories {
  mavenLocal()
  mavenCentral()
  maven {
    url 'https://papermc.io/repo/repository/maven-public/'
  }
  maven {
    url "https://git.timesnake.de/api/v4/groups/7/-/packages/maven"
    name "TimeSnake"
    credentials {
      username = timesnakeUser
      password = timesnakePassword
    }
  }
}

dependencies {
  implementation 'org.mariadb.jdbc:mariadb-java-client:3.0.6'

  compileOnly "de.timesnake:basic-proxy:1.3"

  compileOnly "de.timesnake:database-proxy:2.2.1"
  compileOnly "de.timesnake:database-api:2.2.1"

  compileOnly "de.timesnake:channel-proxy:3.0.5"
  compileOnly "de.timesnake:channel-api:3.0.5"

  compileOnly "de.timesnake:library-extension:1.2"
  compileOnly "de.timesnake:library-basic:1.2.2"
  compileOnly "de.timesnake:library-chat:1.1"

  compileOnly "com.velocitypowered:velocity-api:3.1.1"
  annotationProcessor "com.velocitypowered:velocity-api:3.1.1"
}

configurations.configureEach {
  resolutionStrategy.dependencySubstitution {
    if (project.parent != null) {
      substitute module("de.timesnake:basic-proxy") using project(":basic-proxy")

      substitute module("de.timesnake:database-proxy") using project(":database-proxy")
      substitute module("de.timesnake:database-api") using project(":database-api")

      substitute module("de.timesnake:channel-proxy") using project(":channel-proxy")
      substitute module("de.timesnake:channel-api") using project(":channel-api")

      substitute module("de.timesnake:library-extension") using project(":library-extension")
      substitute module("de.timesnake:library-basic") using project(":library-basic")
      substitute module("de.timesnake:library-chat") using project(":library-chat")
    }
  }
}

tasks.register('exportAsPlugin', Copy) {
  from "$buildDir/libs/${archivesBaseName}-${project.version}.jar"
  into "${timesnakePluginDir}"

  dependsOn(assemble)
}

jar.finalizedBy(shadowJar)

publishing {
  repositories {
    maven {
      url "https://git.timesnake.de/api/v4/projects/" + projectId + "/packages/maven"
      credentials(HttpHeaderCredentials) {
        name = "Job-Token"
        value = System.getenv("CI_JOB_TOKEN")
      }
      authentication {
        header(HttpHeaderAuthentication)
      }
    }
  }

  publications {
    mavenJava(MavenPublication) {
      from components.java
    }
  }
}

tasks.withType(JavaCompile).configureEach {
  options.encoding = 'UTF-8'
}

java {
  withSourcesJar()
}